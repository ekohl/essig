parser grammar TParser;

options {

    // Default language but name it anyway
    //
    language  = Java;

    // Produce an AST
    //
    output    = AST;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractTParser;

    // Use the vocabulary generated by the accompanying
    // lexer.
    //
    tokenVocab = TLexer;
}

// Some imaginary tokens for tree rewrites
//
tokens {
	PARAMS;
	ARGUMENTS;
	EXPR;
}

// What package should the generated source exist in?
//
@header {
    package nl.utwente.cs.essig;
}

@members {
    private int gprs = 0;
    private int gprs_offset = 0;
}

// Parser
//
microcontroller:	IDENTIFIER^ LBRACK! parameters registers instructions RBRACK! EOF!
	;

parameters:		PARAMETERS^ LBRACK! (parameter LINE_SEPERATOR!)+ RBRACK!
	;

parameter:		RAM^ NUMBER
	|		GPRS amount=NUMBER {
				gprs = Integer.parseInt($amount.text);
			}
			(ADD offset=NUMBER {
				gprs_offset = Integer.parseInt($offset.text);
			})?
		->
	|		OP_SIZE^ NUMBER
	|		CLOCK^ NUMBER
	;

registers:		REGISTERS^ LBRACK! (register LINE_SEPERATOR!)+ RBRACK! {
			// Hack in the general purpose registers
			for(int i=0; i < gprs; i++) {
				CommonTree reg = (CommonTree) adaptor.create(IDENTIFIER, "R" + Integer.toString(i));
				adaptor.becomeRoot($REGISTERS.tree, reg);
				adaptor.addChild(reg, adaptor.create(NUMBER, Integer.toString(gprs_offset + i)));
			}
	}
	;

register:		IDENTIFIER^ ASSIGN! NUMBER
	;

instructions:		INSTRUCTIONS^ LBRACK! instruction+ RBRACK!
	;

instruction:		IDENTIFIER params arguments? LBRACK expr+ RBRACK
		-> ^(IDENTIFIER params ^(ARGUMENTS arguments?) ^(EXPR expr+))
	;

params	:		OPCODE (ARG_SEPERATOR OPCODE)* (ARG_SEPERATOR CLOCK ASSIGN NUMBER)?
		-> ^(PARAMS OPCODE+ (^(CLOCK NUMBER))?)
	;

arguments:		argument (ARG_SEPERATOR! argument)*
	;

argument:		(SIGNED)? IDENTIFIER
	;

expr	:		assignExpr LINE_SEPERATOR!
	|		ifExpr
	|		HALT LINE_SEPERATOR!
	;

assignExpr:		(CONSTANT? IDENTIFIER | (RAM LPAREN! operatorExpr RPAREN!)) ASSIGN^ operatorExpr
	;

ifExpr:			IF^ condition LBRACK! expr+ RBRACK! (ELSE LBRACK! expr+ RBRACK!)?
	;

operatorExpr:		word (operator^ operatorExpr)?
	;

condition:		word EQUALS^ word
	|		LPAREN! operatorExpr RPAREN! EQUALS^ word
	;

word	:		NOT? CONSTANT? IDENTIFIER^
				(LPAREN! (IDENTIFIER|NUMBER) RPAREN!)?
	|		NUMBER
	|		RAM^ LPAREN! operatorExpr RPAREN!
	;

operator:		AND | OR | XOR | ADD | MINUS | MULT
	;
